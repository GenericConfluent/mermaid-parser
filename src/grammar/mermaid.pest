WHITESPACE = _{ " " | "\t" }
comment = {
    "%%" ~ (!NEWLINE ~ ANY)*
}
note = {
    "note" ~ "for" ~ class_identifier ~ quoted_text
  | "note" ~ quoted_text
}
direction = {
    "direction" ~ direction_value
}
direction_value = { "TB" | "TD" | "BT" | "RL" | "LR" }
quoted_text = { "\"" ~ (!"\"" ~ ANY)* ~ "\"" }
variable_identifier = @{
    (ASCII_ALPHA | "_") ~
    ((("_"|"-") ~ &ASCII_ALPHANUMERIC)
        | ASCII_ALPHANUMERIC
    )*
}

method_identifier = @{
    ASCII_ALPHA
    ~
    ((("_"|"-") ~ &ASCII_ALPHANUMERIC)
    | ASCII_ALPHANUMERIC
    )*
}

backtick_identifier = @{
    "`" ~ (!"`" ~ ANY)+ ~ "`"
}

plain_identifier = @{
    ASCII_ALPHA
    ~
    ((("_"|"-") ~ &ASCII_ALPHANUMERIC)
    | ASCII_ALPHANUMERIC
    )*
}

class_identifier = @{
    backtick_identifier | plain_identifier
}

class = { "class" ~ class_identifier }

class_label = {
    class ~ "[\"" ~ (!"\"" ~ ANY)* ~ "\"]"
}
class_property = {
    class_identifier ~ ":" ~ visibility? ~ (
        class_identifier ~ variable_identifier
      | variable_identifier
    )
}

class_method = {
    class_identifier
    ~ ":"
    ~ visibility?
    ~ method_identifier
    ~ method_parameter
}

parameter        = { class_identifier ~ variable_identifier
                   | variable_identifier }

parameter_list   = { parameter ~ ("," ~ parameter)* }
method_parameter = { "(" ~ parameter_list? ~ ")" }


diagram = {
   SOI
   ~ "classDiagram"
   ~ NEWLINE+
   ~ statement
   ~ EOI
}

statement = _{((class | relation_stmt | member_stmt | note | direction | comment) ~ NEWLINE*)*}


relation_stmt = {
    class_identifier ~ cardinality? ~ relation ~ cardinality? ~ class_identifier ~ relation_label?
}

cardinality = { quoted_text }
relation_label = { ":" ~ (!NEWLINE ~ ANY)+ }

relation = _{ aggregation_left | aggregation_right
            | composition_left | composition_right
            | inheritance_left | inheritance_right
            | realization_left | realization_right
            | association_left | association_right
            | dependency_left | dependency_right
            | link }

aggregation_left    = { "o--" }
aggregation_right   = { "--o" }
composition_left    = { "*--" }
composition_right   = { "--*" }
inheritance_left    = { "<|--" }
inheritance_right   = { "--|>" }
realization_left    = { "<|.." }
realization_right   = { "..|>" }
association_left    = { "<--" }
association_right   = { "-->" }
dependency_left     = { "<.." }
dependency_right    = { "..>" }
link                = { "--" }

member_stmt       = { class_identifier ~ ":" ~ member_decl }
member_decl       = { class_method_decl | class_property_decl }
class_method_decl = { visibility? ~ classifier? ~ method_identifier ~ method_parameter ~ (":" ~ class_identifier)? }
class_property_decl = { visibility? ~ classifier? ~ (class_identifier ~ variable_identifier | variable_identifier) }


visibility = @{ public | private | protected | package }
public     = _{ "+" }
private    = _{ "-" }
protected  = _{ "#" }
package    = _{ "~" }


classifier = _{abstract | static}
abstract = { "*" }
static = { "$" }


// Tests

test_class_explicit = {
    SOI ~ class ~ EOI
}
test_class_labels = {
    SOI ~ class_label ~ EOI
}
test_class_property = {
    SOI ~ class_property ~ EOI
}
test_class_method = {
    SOI ~ class_method ~ EOI
}
test_comment = {
    SOI ~ comment ~ EOI
}
test_note = {
    SOI ~ note ~ EOI
}
test_direction = {
    SOI ~ direction ~ EOI
}